# ###################################################################
# Mp3tag Tag Source for VocaDB
# Romaji tags source
#
# Search by ALBUM
#
# This file should be stored in your tag sources sources directory
# %APPDATA%\Mp3tag\data\sources
# and requires Mp3tag v2.64 or above.
#
# [2015-08-31] v1.00
#
# [2021-11-05] v1.10
# - now adds DISCNUMBER and TRACK fields for albums with multiple discs
# [2021-11-08] v1.13
# - now adds JAPANESETITLE and JAPANESEALBUM tags using the defaultName fields (usually in Japanese)
#
# [2022-01-19] v1.20
# Renamed to VocaDB#&Romaji Tags.src. You can find it in the VocaDB side menu.
# Added tags:
# UNSYNCEDLYRICS: tags Romaji lyrics to each song
# VOCADB_TRACK_ID: tags vocadb track id to each song
#
# [2022-01-19] v1.21
# Add month and day to year tag
# 
# [2022-03-23] v1.22
# Now adds " (Instrumental)" to Instrumental song titles.
#
# (further updates will be listed on the git commit log: https://github.com/Onurtag/Mp3tag-Sources )
# 
# [2024-01-15] v1.23
# Howdy! Pitgram here. I modified the script, so that it uses English instead of romaji and so that it inserts both ALBUMARTIST and ARTIST in a format that is compatible with id3v2's multiple artists
#
# [2024-01-29]
# Fixed date, redesigned album listing, added autofill rules, added english album name, fixed track counting
#
# [2024-02-14]
# Release events are now inserted into VOCADB_RELEASE_EVENT custom tag, multiple events are supported and by default are separated by "\\" separator, to change this to for example "; " look into "# Release event" section of the code
# 
# ###################################################################

[Name]=VocaDB
[BasedOn]=vocadb.net
[IndexUrl]=https://vocadb.net/api/albums?fields=AdditionalNames,Tags,Tracks,Names,ReleaseEvent,MainPicture&nameMatchMode=Auto&maxResults=50&preferAccurateMatches=true&getTotalCount=true&lang=English&query=%s
[AlbumUrl]=https://vocadb.net/api/albums/
[WordSeparator]=%20
[IndexFormat]=%Artist%|%Album%|%Original%|%Release_Date%|%Catalog_Number%|%_preview%|%_url%|%tracks%
[SearchBy]=Album||$if2(%album%,%title%)||
[UserAgent]=1
[Encoding]=url-utf-8

[ParserScriptIndex]=...
# ###################################################################
#					I  N  D  E  X
# ###################################################################

# Debug "on" "C:\\KeepThisEmpty\\vocadb_english_DEBUG.json"
# DebugWriteInput "C:\\KeepThisEmpty\\vocadb_english.json"

# replace problematic characters for Mp3tag
replace "|" "$verticalBar()"
# use "current" json (applies replacements above)
json "ON" "current"

json_select "totalCount"

ifnot "0"
	json_foreach "items"
		
		# Artist
		json_select "artistString"
		#sayregexp ".+?(?= -.*)"
		sayrest
		say "|" 

		# Album
		json_select "name"
		#sayregexp "(?<= - ).*"
		sayrest
		say "|"
		
		# Original
		json_select "defaultName"
		#sayregexp "(?<= - ).*"
		sayrest
		say "|"

		# ReleaseDate
		json_select_object "releaseDate"
			json_select "year"
			sayrest
			say "."
			json_select "month"
			sayrest
			say "."
			json_select "day"
			sayrest
			say "|"
		json_unselect_object

		# Catalog_Number
		json_select "catalogNumber"
		sayrest
		say "|"

		# Preview
		say "https://vocadb.net/Al/"
		json_select "id"
		sayrest
		say "|"

		# URL
		json_select "id"
		sayrest
		say "?fields=Tracks,Names,ReleaseEvent&lang=English&songFields=Lyrics"
		say "|"

		# Tracks
		set "total_tracks"
		json_select_many_count "tracks" "trackNumber"
		outputto "total_tracks"
		outputto "Output"
		sayrest
		saynewline

	json_foreach_end
endif

[ParserScriptAlbum]=...
# ###################################################################
#					A  L  B  U  M
# ###################################################################

# Debug "on" "C:\\KeepThisEmpty\\VOCADB_english_single_DEBUG.json"
# DebugWriteInput "C:\\KeepThisEmpty\\VOCADB_english_single.json"

# replace problematic characters for Mp3tag
replace "|" "$verticalBar()"
# use "current" json (applies replacements above)
json "ON" "current"

json_select_object "items"

	# Album
	outputto "ALBUM"
	json_select "name"
	sayrest

	# Default Album Name (JP) into JAPANESEALBUM tag
	outputto "JAPANESEALBUM"
	json_select "defaultName"
	sayrest

	# VOCADB_ID
	outputto "VOCADB_ID"
	json_select "id"
	sayrest

	# Release event
		outputto "VOCADB_RELEASE_EVENT"
		json_select_many "releaseEvents" "name" "\\\\"
		sayrest

	# Coverurl
	outputto "coverurl"
	json_select "id"
	say "https://vocadb.net/Album/CoverPicture/"
	sayrest

	# Catalog Number
	outputto "CATALOGID"
	json_select "catalogNumber"
	#json_select_many "labels" "catno" ", "
	sayrest

	# Publisher
	outputto "PUBLISHER"
	json_select_many "labels" "name" ", "
	sayrest

	# Media Type
	outputto "MEDIATYPE"
	json_select "discType"
	sayrest

	# Year
	outputto "YEAR"
	json_select_object "releaseDate"
		json_select "year"
		sayrest
		say "."
		json_select "month"
		sayrest
		say "."
		json_select "day"
		sayrest
	json_unselect_object

	outputto "ALBUMARTIST"
	json_select "artistString"
	replace " feat. " "\\\\"
	replace ", " "\\\\"
	sayrest

	# Names and sortnames
	json_foreach "names"
		json_select "language"
		if "English"
			outputto "ALBUMSORT"
			json_select "value"
			sayrest
		endif
	json_foreach_end

	# Tracks
	json_foreach "tracks"
		outputto "DISCNUMBER"
		json_select "discNumber"
		sayrest
		say "|"

		outputto "TRACK"
		json_select "trackNumber"
		sayrest
		say "|"

		outputto "TRACKS"
		json_select "name"
		sayrest
		# # Don't say | here because we do it after (Instrumental)
		# say "|"

		json_select_object "song"
			# VOCADB_TRACK_ID
			outputto "VOCADB_TRACK_ID"
			json_select "id"
			sayrest
			say "|"

			outputto "_LENGTH"
			json_select "lengthSeconds"
			sayrest
			say "|"

			outputto "Artist"
			json_select "artistString"
			replace " feat. " "\\\\"
			replace ", " "\\\\"
			sayrest
			say "|"

			# Append " (Instrumental)" to the title of instrumental tracks
			outputto "TRACKS"
			json_select "songType"
			if "Instrumental"
				say " (Instrumental)"
			endif
			say "|"

			# Default Title (JP) into JAPANESETITLE tag
			outputto "JAPANESETITLE"
			json_select "defaultName"
			sayrest
			say "|"

			# Romanized Lyrics into UNSYNCEDLYRICS tag
			#START lyrics block
			outputto "UNSYNCEDLYRICS"
			json_foreach "lyrics"
				ifnot "0"
					#parse lyric types for translationType: "Original"
					json_select "translationType"
					if "Romanized"
						json_select "value"
						sayrest
					endif
				endif
			json_foreach_end
			say "|"
			#END lyrics block

		json_unselect_object
	json_foreach_end

	set "tmp_join"

json_unselect_object