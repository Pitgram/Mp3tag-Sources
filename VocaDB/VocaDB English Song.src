[Name]=VocaDB
[BasedOn]=vocadb.net
[IndexUrl]=https://vocadb.net/api/songs?fields=Names,Albums,Lyrics,MainPicture&nameMatchMode=Auto&start=0&artistParticipationStatus=Everything&maxResults=333&sort=FavoritedTimes&preferAccurateMatches=true&getTotalCount=true&lang=English
[AlbumUrl]=https://vocadb.net/api/songs/
[WordSeparator]=%20
[IndexFormat]=%_coverurl%|%Name%|%Artist%|%Song Type%|%Albums%|%ReleaseDate%|%_preview%|%_url%|
[SearchBy]=Title||%title%||&query=%s||VocaDB Artist Code||||&artistId%5B%5D=%s
[UserAgent]=1
[Encoding]=url-utf-8
[ParserScriptIndex]=...

# ###################################################################
#					I  N  D  E  X
# ###################################################################

# Debug "on" "C:\\temp\\vocadb_english_DEBUG.json"
# DebugWriteInput "C:\\temp\\vocadb_english.json"

# replace problematic characters for Mp3tag
replace "|" "$verticalBar()"
# use "current" json (applies replacements above)
json "ON" "current"

json_select "totalCount"

ifnot "0"
	json_foreach "items"
		
		# Coverurl
		json_select_object "mainPicture"
			json_select "urlThumb"
			sayrest
			say "|"
		json_unselect_object
		
		# Name
		json_select "name"
		#sayregexp "(?<= - ).*"
		sayrest
		say "|"		
		
		# Artist
		json_select "artistString"
		#sayregexp ".+?(?= -.*)"
		sayrest
		say "|" 
		
		# Type
		json_select "songType"
		#sayregexp "(?<= - ).*"
		sayrest
		say "|"	
		
		# Albums
		json_foreach "albums"
			say "\""
			json_select "name"
			sayrest
			say "\" "
		json_foreach_end
		say "|"
		
		# ReleaseDate
		json_select "publishDate"
		#sayregexp "(?<= - ).*"
		SayUntil "T"
		say "|"

		# Preview
		say "https://vocadb.net/S/"
		json_select "id"
		sayrest
		say "|"

		# URL
		json_select "id"
		sayrest
		say "?fields=Albums,Lyrics,MainPicture,Names&lang=English&songFields=Lyrics"
		say "|"
		sayrest
		saynewline

	json_foreach_end
endif

[ParserScriptAlbum]=...
# ###################################################################
#					A  L  B  U  M
# ###################################################################

# Debug "on" "C:\\temp\\vocadb_english_single_DEBUG.json"
# DebugWriteInput "C:\\temp\\vocadb_english_single.json"

# replace problematic characters for Mp3tag
replace "|" "$verticalBar()"
# use "current" json (applies replacements above)
json "ON" "current"

json_select_object "items"

	# Album
	outputto "ALBUM"
	json_select "name"
	sayrest

	# Default Album Name (JP) into JAPANESEALBUM tag
	outputto "JAPANESEALBUM"
	json_select "defaultName"
	sayrest

	# Coverurl
	outputto "coverurl"
	json_select_object "mainPicture"
		json_select "urlOriginal"
		sayrest
	json_unselect_object

	# Catalog Number
	outputto "CATALOGID"
	json_select "catalogNumber"
	#json_select_many "labels" "catno" ", "
	sayrest

	# Publisher
	outputto "PUBLISHER"
	json_select_many "labels" "name" ", "
	sayrest

	# Media Type
	outputto "MEDIATYPE"
	json_select "discType"
	sayrest

	# Year
	outputto "YEAR"
	json_select "publishDate"
	replace "-" "."
	SayUntil "T"

	outputto "ALBUMARTIST"
	json_select "artistString"
	replace " feat. " "\\\\"
	replace ", " "\\\\"
	sayrest

	# Names and sortnames
	json_foreach "names"
		json_select "language"
		if "English"
			outputto "ALBUMSORT"
			json_select "value"
			sayrest
		endif
	json_foreach_end

	# Tracks
	json_foreach "tracks"
		outputto "DISCNUMBER"
		json_select "discNumber"
		sayrest
		say "|"

		outputto "TRACK"
		json_select "trackNumber"
		sayrest
		say "|"

		outputto "TRACKS"
		json_select "name"
		sayrest
		# # Don't say | here because we do it after (Instrumental)
		# say "|"
			
		json_select_object "song"
			# VOCADB_TRACK_ID
			outputto "VOCADB_TRACK_ID"
			json_select "id"
			sayrest
			say "|"

			outputto "_LENGTH"
			json_select "lengthSeconds"
			sayrest
			say "|"

			outputto "Artist"
			json_select "artistString"
			replace " feat. " "\\\\"
			replace ", " "\\\\"
			sayrest
			say "|"

			# Append " (Instrumental)" to the title of instrumental tracks
			outputto "TRACKS"
			json_select "songType"
			if "Instrumental"
				say " (Instrumental)"
			endif
			say "|"

			# Default Title (JP) into JAPANESETITLE tag
			outputto "JAPANESETITLE"
			json_select "defaultName"
			sayrest
			say "|"

			# Romanized Lyrics into UNSYNCEDLYRICS tag
			#START lyrics block
			outputto "UNSYNCEDLYRICS"
			json_foreach "lyrics"
				ifnot "0"
					#parse lyric types for translationType: "Original"
					json_select "translationType"
					if "Romanized"
						json_select "value"
						sayrest
					endif
				endif
			json_foreach_end
			say "|"
			#END lyrics block

		json_unselect_object
	json_foreach_end

	set "tmp_join"

	###
	# VocaDB specific tags
	###

	# Release event
	outputto "VOCADB_RELEASE_EVENT"
	json_select "releaseEvent"
	sayrest
json_unselect_object